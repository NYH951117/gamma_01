#! /bin/tcsh -fe
###########################################################################
###     GC-MAP: script to run DIFF&GEO program sequence to  create      ###
###             refined geocoding lookup table                          ###                                
###########################################################################
### uw  020730
### clw 040414  modified output file name offset_fitm
### clw 050428  modified for Win32 compatibility creating diff_par.in
###
###########################################################################
echo " "
echo "GC-MAP: refined lookup table derivation for terrain corrected SAR geocoding"

if ($#argv < 10)then
  echo " "
  echo "usage: GC_MAP <SLC_par> <OFF_par> <DEM_par> <lat_ovr> <lon_ovr> <DEM_gc_par> <DEM_gc> <pwr> <MAP_to_RDC> [inc] [pix] [ls_map]"
  echo "  "
  echo "       SLC_par     (input) reference slc/mli geometry parameter file"
  echo "       OFF_par     (input) ISP offset/interferogram parameter file, enter - if geocoding SLC or MLI data"
  echo "       DEM_par     (input) DEM parameter file"
  echo "       DEM         (input) DEM data file"
  echo "       lat_ovr     latitude DEM oversampling factor"
  echo "       lon_ovr     longitude DEM oversampling factor"
  echo "       DEM_gc_par  (output, and if existing input) DEM segment parameter file used for geocoding"
  echo "       DEM_gc      (output) segment of DEM used for geocoding"
  echo "                   interpolated if lat_ovr > 1  or lon_ovr > 1"
  echo "       pwr         (input) SAR intensity image (for fine registration)"
  echo "       MAP_to_RDC  (output) refined geocoding lookup table"
  echo "       inc         (output) local incidence angle (between surface normal and look vector) (in map geometry)"
  echo "       pix         (output) pixel area normalization factor (in map geometry)"
  echo "       ls_map      (output) layover and shadow map (in map geometry)"
  echo " "
  echo "Remark: select - as filename to avoid creation of corresponding output file"
  exit
endif

set slc_par = $1
set off = $2
set DEM_par = $3
set DEM = $4
set lat_ovr = $5
set lon_ovr = $6
set DEM_gc_par = $7
set DEM_gc = $8
set pwr = $9
set MAP_to_RDC = $10

set inc = "-"
if ($#argv > 11)set inc = $11
set pix = "-"
if ($#argv > 12)set pix = $12
set ls_map = "-"
if ($#argv > 13)set ls_map = $13

set map_to_rdc = "map_to_rdc" 			# initial geocoding lookup table
set pwr_sim = "pwr_sim" 			# simulated SAR intensity image in rdc
set pwr_sim_map = "pwr_sim_map" 		# simulated SAR intensity image in map geometry
set hgt_sim = "hgt_sim" 			# geocoded height generated with map_to_rdc
set diff_par = "diff_par" 			# Create (DIFF&GEO-)DIFF&GEO parameter file
set offs = "offs"
set snr = "snr"
set offsets = "offsets"
set coffs = "coffs"
set coffsets = "coffsets"

if ( "$off" == "-" ) then
  set width  = `awk '$1 == "range_samples:" {print $2}' $slc_par`
  set nlines = `awk '$1 == "azimuth_lines:" {print $2}' $slc_par`
else
  set width  = `awk '$1 == "interferogram_width:" {print $2}' $off`
  set nlines  = `awk '$1 == "interferogram_azimuth_lines:" {print $2}' $off`
endif

###########################################################################
#
###########################################################################
###   Initial Geocoding lookup table derivation:                       ###
      #########################################
if(1) then
  echo ""
  echo "Generation of initial geocoding lookup table"
  echo "gc_map $slc_par $off $DEM_par $DEM $DEM_gc_par $DEM_gc $map_to_rdc $lat_ovr $lon_ovr $pwr_sim_map - - $inc - $pix $ls_map"
  nice gc_map $slc_par $off $DEM_par $DEM $DEM_gc_par $DEM_gc $map_to_rdc $lat_ovr $lon_ovr $pwr_sim_map - - $inc - $pix $ls_map
  if(-e "-")/bin/rm "-"              
endif

set dem_width  = `awk '$1 == "width:" {print $2}' $DEM_gc_par`
set dem_nlines  = `awk '$1 == "nlines:" {print $2}' $DEM_gc_par`

echo "DEM segment width after oversampling:           $dem_width"
echo "DEM segment number of lines after oversampling: $dem_nlines"


###########################################################################
###   transformation of simulated SAR image from map to SAR geometry:   ###
      ##############################################################
if(1) then
  echo ""
  echo "transformation of simulated SAR image from map to SAR geometry"
  nice geocode $map_to_rdc $pwr_sim_map $dem_width $pwr_sim $width $nlines 0 0    
endif              

###########################################################################
###   Create (DIFF&GEO-)DIFF&GEO parameter file:                        ###
      ##########################################
if(1) then
  echo ""
  if (-e $diff_par) /bin/rm $diff_par
  if( "$off" == "-" )then
    echo "Create DIFF&GEO parameter file from SLC/MLI parameter file"
    echo 'GC_MAP 0 0 32 32 128 128 7.0' > diff_par.in
          create_diff_par $slc_par $slc_par $diff_par 1 < diff_par.in
  else
    echo "Create DIFF&GEO parameter file from ISP/offset parameter file"
    echo 'GC_MAP 0 0 32 32 128 128 7.0' > diff_par.in
          create_diff_par $off $off $diff_par 0 < diff_par.in
  endif  
endif              

###########################################################################
###   Fine registration (pwr_sim used as reference geom.)               ###
      ###################################################
if(1) then
  echo ""
  echo "Fine registration (pwr_sim used as reference geom.)"
###  echo "init_offsetm $pwr_sim $pwr $diff_par 2 2"
###  nice init_offsetm $pwr_sim $pwr $diff_par 2 2
###  echo ""
  if(0) then  ### first round
    echo "offset_pwrm $pwr_sim $pwr $diff_par $offs $snr 256 256 $offsets 1 16 16"
    nice offset_pwrm $pwr_sim $pwr $diff_par $offs $snr 256 256 $offsets 1 16 16
    echo ""
    echo "offset_fitm $offs $snr $diff_par $coffs $coffsets 7.0"
    nice offset_fitm $offs $snr $diff_par $coffs $coffsets 7.0
  endif              
  echo ""
  echo "offset_pwrm $pwr_sim $pwr $diff_par $offs $snr 128 128 $offsets 2 32 32"
  nice offset_pwrm $pwr_sim $pwr $diff_par $offs $snr 128 128 $offsets 2 32 32
  echo ""
  echo "offset_fitm $offs $snr $diff_par $coffs $coffsets 7.0"
  nice offset_fitm $offs $snr $diff_par $coffs $coffsets 7.0 > offset_fitm.out
  #if(-e "$offs")/bin/rm "$offs"              
  #if(-e "$snr")/bin/rm  "$snr"              
  #if(-e "$offsets")/bin/rm  "$offsets"              
  #if(-e "$coffs")/bin/rm  "$coffs"              
  #if(-e "$coffsets")/bin/rm  "$coffsets"              
endif              

###########################################################################
###   Refine lookup table with registration offset polynomials          ###
      ########################################################
if(1) then
  echo ""
  echo "Refine lookup table with registration offset polynomials"
  echo "gc_map_fine $map_to_rdc $dem_width $diff_par $MAP_to_RDC 1"
  nice gc_map_fine $map_to_rdc $dem_width $diff_par $MAP_to_RDC 1
  #if ((-e "$MAP_to_RDC") && (-e "$map_to_rdc"))/bin/rm  "$map_to_rdc"              
endif              
    
###                                                                     ###
###########################################################################

###########################################################################
###   Quality control                                                   ###
      ###############
if ( (-e "offset_fitm.out") ) then
  echo ""
  echo "GC_MAP Quality control:"
  echo "***********************"
  grep "final" offset_fitm.out
  echo ""
endif              

echo End of GC_MAP
    
###                                                                     ###
###########################################################################

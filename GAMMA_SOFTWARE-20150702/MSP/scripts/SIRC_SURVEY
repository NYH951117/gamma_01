#! /bin/csh -fe
echo "SIRC_SURVEY: SIR-C survey processing v3.1 11-May-2000 clw/uw"
if ($#argv < 2)then
  echo "usage: SIRC_SURVEY <SIRC_sensor> <scene> [RAW_dir] [RC_dir]"
  echo "       SIRC_sensor     SIR-C sensor parameter file name (SIRC.par)"
  echo "       scene           scene identifier (example: orbit number)"
  echo "       RAW_dir         directory for the raw data file"
  echo "       RC_dir          directory for the range compressed file"  
  echo "       SLC_dir         Single Look Complex image file directory (enter . for current directory)"
  exit
endif

set RAW = "."
if ($#argv >= 3) then 
  set RAW = $3
endif

set RC = "."
if ($#argv >= 4) then 
  set RC = $4
endif

set SLC = "."
if ($#argv >= 5) then 
  set SLC = $5
endif

# tests if required input files exist:
if(1) then
  echo "test if required input/output files and directories exist"
  if (-e "$1" == 0 ) then 
    echo "ERROR: sensor parameter file ($1) does not exist"
    exit(-1)
  endif 
  if ((-e "p$2.slc.par" == 0 )) then 
    echo "ERROR: MSP processing parameter file (p$2.slc.par) does not exist"
    exit(-1)
  else
    if ((-e "p$2.survey.slc.par" == 0 )) then 
      echo "MSP processing parameter file (p$2.slc.par) copied to (p$2.survey.slc.par)"
      /bin/cp p$2.slc.par p$2.survey.slc.par
    endif
  endif
  if (($#argv >= 3) && (-e "$3" == 0 )) then 
    echo "ERROR: RAW_dir ($3) does not exist"
    exit(-1)
  endif 
  if ((-e "$RAW/$2.raw" == 0 ) && (-e "$RAW/$2.fix" == 0 )) then 
    echo "ERROR: Raw data file ($RAW/$2.raw or $RAW/$2.fix) does not exist"
    exit(-1)
  endif 
  if( ($#argv >= 4) && (-e "$4" == 0 ) ) then
    echo "ERROR: RC_dir ($4) does not exist"
    exit(-1)
  endif 
  if( ($#argv >= 5) && (-e "$5" == 0 ) ) then 
    echo "ERROR: SLC_dir ($5) does not exist"
    exit(-1)
  endif 
  echo "required input/output files and directories exist"
endif

set SENSOR = $1

set DECIM = 10
set NSUB = 16
set AMB_FLAG = 1
set RLK = 10
set AZLK = 1
set AZ_PATCH = 2048
set MLI_DECIM = `echo $DECIM $AZLK |awk '{print $1*$2}'`
set AZ_PROC = az_proc
set ANTENNA = constant_antenna.gain

set CAL_FACT = "0.0"

# check if fixed raw data file exists (missing lines, range gate etc.)

if (-e $RAW/$2.fix) then
  set RAW_DATA = $RAW/$2.fix
else
  set RAW_DATA = $RAW/$2.raw
endif
 
echo " "
echo "raw data decimation factor:  $DECIM"
echo "number of range looks:       $RLK"
echo "number of azimuth looks:     $AZLK"
echo "azimuth patch size:          $AZ_PATCH"
echo "azimuth processing program:  $AZ_PROC"
echo "antenna pattern filename:    $ANTENNA"
echo "raw data file:               $RAW_DATA"
echo "range compressed data:       $RC/$2.rc"
echo "decimated range comp. data:  $RC/$2.prc"
echo "survey SLC file:             $2.survey.slc"
echo "survey Multi-Look Intensity: $2.survey.mli"

echo ""
rm -f doppler_$2.out
echo "doppler_real $SENSOR p$2.survey.slc.par $RAW_DATA $2.dop - $NSUB $AMB_FLAG > doppler_$2.out"
doppler_real $SENSOR p$2.survey.slc.par $RAW_DATA $2.dop - $NSUB $AMB_FLAG > doppler_$2.out

echo ""
rm -f rspec_$2.out
echo "rspec_real $SENSOR p$2.survey.slc.par $RAW_DATA $2.rspec > rspec_$2.out"
rspec_real $SENSOR p$2.survey.slc.par $RAW_DATA $2.rspec > rspec_$2.out

echo ""
rm -f prefilt_$2.out
echo "prefilt $SENSOR p$2.survey.slc.par $RC/$2.rc $RC/$2.prc $DECIM > prefilt_$2.out"
prefilt $SENSOR p$2.survey.slc.par $RC/$2.rc $RC/$2.prc $DECIM > prefilt_$2.out

echo ""
rm -f az_$2.survey.out
echo "$AZ_PROC $SENSOR p$2.survey.slc.par $RC/$2.rc $2.survey.slc $AZ_PATCH 0 $CAL_FACT 0  > az_$2.survey.out"
$AZ_PROC $SENSOR p$2.survey.slc.par $RC/$2.rc $2.survey.slc $AZ_PATCH 0 $CAL_FACT 0  > az_$2.survey.out

echo ""
rm -f mli_$2.survey.out
echo "multi_SLC p$2.survey.slc.par p$2.survey.mli.par $2.survey.slc $2.survey.mli $RLK $AZLK > mli_$2.survey.out"
multi_SLC p$2.survey.slc.par p$2.survey.mli.par $2.survey.slc $2.survey.mli $RLK $AZLK > mli_$2.survey.out

#echo ""
#echo "rm -f $2.prc"
#rm -f $2.prc
